//
// build.gradle in TeamCode
//
// Most of the definitions for building your module reside in a common, shared
// file 'build.common.gradle'. Being factored in this way makes it easier to
// integrate updates to the FTC into your code. If you really need to customize
// the build definitions, you can place those customizations in this file, but
// please think carefully as to whether such customizations are really necessary
// before doing so.

import org.jetbrains.kotlin.gradle.dsl.JvmTarget

// Custom definitions may go here
buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://repo.dairy.foundation/releases"
        }
    }
    dependencies {
        classpath "dev.frozenmilk:Load:0.2.4"
    }
}

plugins {
    id 'org.jetbrains.kotlin.android' version '2.2.21'
    id 'dev.zacsweers.metro' version '0.7.5'
}

// Include common definitions from above.
apply from: '../build.common.gradle'
apply from: '../build.dependencies.gradle'
apply plugin: 'dev.frozenmilk.sinister.sloth.load'

android {
    namespace = 'org.firstinspires.ftc.teamcode'

    kotlin {
        compilerOptions {
            jvmTarget = JvmTarget.JVM_11
            freeCompilerArgs = ["-XXLanguage:+PropertyParamAnnotationDefaultTargetMode"]
        }
        compileOptions {
            sourceCompatibility = JavaVersion.VERSION_11
            targetCompatibility = JavaVersion.VERSION_11
        }
    }

    packagingOptions {
        jniLibs.useLegacyPackaging true
    }
}

repositories {
    maven { url = "https://repo.dairy.foundation/releases" }
    maven { url = "https://mymaven.bylazar.com/releases" }
    maven { url = "https://maven.brott.dev/" }
    mavenLocal()
}

metro {
    contributesAsInject = true
}

dependencies {
    implementation project(':FtcRobotController')
    implementation project(':PedroPathingDSL')
    implementation 'com.pedropathing:ftc:2.0.4'
    implementation 'com.pedropathing:telemetry:1.0.0'
    implementation("dev.frozenmilk.sinister:Sloth:0.2.4")
    compileOnly("com.acmerobotics.dashboard:dashboard:0.5.1!!")
    annotationProcessor files('lib/OpModeAnnotationProcessor.jar')

    testImplementation platform("org.junit:junit-bom:5.14.1")
    testImplementation("org.junit.jupiter:junit-jupiter")
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}
